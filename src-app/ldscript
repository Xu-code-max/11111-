MEMORY
{
	flash   (rx)  : ORIGIN = 0x00040000, LENGTH = 1M - 0x40000 - 0x4000
	sram0   (rwx) : ORIGIN = 0x20000000, LENGTH = 16K /* boot loader */
	sram1   (rwx) : ORIGIN = 0x20010000, LENGTH = 4K /* ring buffer */
	sram2   (rwx) : ORIGIN = 0x20011000, LENGTH = 64K /* this app */
	sram3   (rwx) : ORIGIN = 0x20021000, LENGTH = 0x1f000 /* malloc */
}

ENTRY(__start)
SECTIONS
{
	. = 0x40000;
	.start . : {
		*start.o(.text);
	} > flash

	.text : {
		*(.exception);
		*(.text*);
	} > flash

	.sysinit : {
		__sysinit_start = ABSOLUTE(.);
		*(.sysinit)
		__sysinit_end = ABSOLUTE(.);
	} > flash

	.rodata : {
		*(.rodata*);
	} > flash

	/* Ensure _smem is associated with the next section */
	. = .;
	_smem = ABSOLUTE(.);
	.data : {
		_sdata = ABSOLUTE(.);
		*(.data*);
		_edata = ABSOLUTE(.);
	} > sram2 AT > flash

	.bss : {
		_sbss = ABSOLUTE(.);
		*(.bss*)
		*(COMMON)
		_ebss = ABSOLUTE(.);
	} > sram2
}
