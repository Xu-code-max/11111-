MEMORY
{
	flash   (rx)  : ORIGIN = 0x01000000, LENGTH = 256K - 0x4000
	sram0   (rwx) : ORIGIN = 0x21000000, LENGTH = 48K
	sram1   (rwx) : ORIGIN = 0x2100c000, LENGTH = 16K /* malloc */
}

ENTRY(md_init)
SECTIONS
{
	. = 0x01000000;
	.start . : {
		*start.o(.text);
	} > flash

	.text : {
		*(.exception);
		*(.text*);
	} > flash

	.sysinit : {
		__sysinit_start = ABSOLUTE(.);
		*(.sysinit)
		__sysinit_end = ABSOLUTE(.);
	} > flash

	.rodata : {
		*(.rodata*);
	} > flash

	/* Ensure _smem is associated with the next section */
	. = .;
	_smem = ABSOLUTE(.);
	.data : {
		_sdata = ABSOLUTE(.);
		*(.data*);
		_edata = ABSOLUTE(.);
	} > sram0 AT > flash

	.bss : {
		_sbss = ABSOLUTE(.);
		*(.bss*)
		*(COMMON)
		_ebss = ABSOLUTE(.);
	} > sram0
}
